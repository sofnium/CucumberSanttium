//import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id 'java'
    id 'se.thinkcode.cucumber-runner' version '0.0.8'
    //id 'io.freefair.lombok' version '6.2.0'
}

ext {
    junitVersion = '5.8.1'
    cucumberVersion = '7.0.0'
}

group 'com.santtium'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    implementation 'org.projectlombok:lombok:1.16.20'

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"

    testImplementation "io.cucumber:cucumber-java:${cucumberVersion}"

    testImplementation "io.cucumber:cucumber-junit:${cucumberVersion}"
    testImplementation "org.junit.vintage:junit-vintage-engine:${junitVersion}"
    //testImplementation group: 'org.projectlombok', name: 'lombok', version: '1.18.22'
    testImplementation group: 'io.rest-assured', name: 'rest-assured', version: '4.4.0'
    testImplementation group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'

    //testImplementation group: 'io.cucumber', name: 'cucumber-picocontainer', version: '7.0.0'
    testImplementation "org.slf4j:slf4j-simple:1.6.1"
    testImplementation files('libs/SanttiumDriverJava.jar')
    testImplementation files('libs/SanttiumReportHelper.jar')
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

task cucumberCli() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = [
              '--plugin', 'pretty',
              '--plugin', 'html:target/cucumber-report.html',
              '--glue', 'stepsDefinitions',
              '--tags', '@tv',
              'src/test/resources']
        }
    }
}

cucumber {
    main = 'io.cucumber.core.cli.Main'
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }

    systemProperties(project.gradle.startParameter.systemPropertiesArgs)
    systemProperty 'key_device', System.getProperty('key_device')
    systemProperty "cucumber.filter.tags", System.getProperty("cucumber.filter.tags")
}
